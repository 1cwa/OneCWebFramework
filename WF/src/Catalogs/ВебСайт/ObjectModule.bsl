#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
			
	Если НЕ ЭтоГруппа Тогда
		
		Если ПроверитьСтраница404() или ПроверитьСтраницуАвторизации() или ПроверитьСтраницаОсновная() тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Расширение = Справочники.ВебСайт.РасширениеБезТочки(Справочники.ВебСайт.ПолучитьРасширениеИмениФайла(Наименование));
		
		Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
		ДвоичныеДанные = ХранилищеФайла.Получить();
		Если ДвоичныеДанные <> Неопределено Тогда
			Хеш.Добавить(ДвоичныеДанные);
		КонецЕсли;
		Хеш.Добавить(АлгоритмОбработки);
		КонтрольнаяСумма = НРег(СтрЗаменить(Хеш.ХешСумма, " ", ""));
		ОтносительнаяСсылка = СформироватьОтносительнуюСсылку();
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьОтносительнуюСсылку() Экспорт
	
	Результат = "";
	Страница = ЭтотОбъект;
	
	Если ЗначениеЗаполнено(Наименование) Тогда
		
		Результат 		= Наименование;
		Пока ЗначениеЗаполнено(Страница.Родитель) Цикл			
			Страница 	= Страница.Родитель;
			Результат 	= Страница.Наименование + "/" + Результат;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьСтраницуАвторизации()
	
	Если ЭтотОбъект.СтраницаАвторизации тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВебСайт.Ссылка КАК Ссылка,
		|	ВебСайт.ОтносительнаяСсылка КАК ОтносительнаяСсылка
		|ИЗ
		|	Справочник.ВебСайт КАК ВебСайт
		|ГДЕ
		|	ВебСайт.СтраницаАвторизации
		|	И ВебСайт.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() цикл		
			Сообщить(СтрШаблон("Страница %1 уже помеченна как страница авторизации.",Выборка.ОтносительнаяСсылка));
			Возврат Истина;			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПроверитьСтраница404()
	
	Если ЭтотОбъект.Страница404 тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВебСайт.Ссылка КАК Ссылка,
		|	ВебСайт.ОтносительнаяСсылка КАК ОтносительнаяСсылка
		|ИЗ
		|	Справочник.ВебСайт КАК ВебСайт
		|ГДЕ
		|	ВебСайт.Страница404
		|	И ВебСайт.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() цикл			
			Сообщить(СтрШаблон("Страница %1 уже помеченна как страница 404.",Выборка.ОтносительнаяСсылка));
			Возврат Истина;			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПроверитьСтраницаОсновная()
	
	Если ЭтотОбъект.СтраницаОсновная тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВебСайт.Ссылка КАК Ссылка,
		|	ВебСайт.ОтносительнаяСсылка КАК ОтносительнаяСсылка
		|ИЗ
		|	Справочник.ВебСайт КАК ВебСайт
		|ГДЕ
		|	ВебСайт.СтраницаОсновная
		|	И ВебСайт.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() цикл			
			Сообщить(СтрШаблон("Страница %1 уже помеченна как основная страница.",Выборка.ОтносительнаяСсылка));
			Возврат Истина;			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции


#КонецОбласти

#КонецЕсли