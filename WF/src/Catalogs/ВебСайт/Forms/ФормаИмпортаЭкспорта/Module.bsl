
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимЗагрузкиПриИзменении(Элемент)
	Элементы.ВыполнитьОперацию.Заголовок = ?(РежимЗагрузки = 0, "Выполнить загрузку", "Выполнить выгрузку");
	Элементы.Путь.Видимость = РежимЗагрузки = 0 или РежимЗагрузки = 1; 
КонецПроцедуры

&НаКлиенте
Процедура ПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(?(РежимЗагрузки = 0, РежимДиалогаВыбораФайла.Открытие, РежимДиалогаВыбораФайла.Сохранение));
	Диалог.Фильтр = НСтр("ru = 'Файл ZIP-архива (*.zip)|*.zip|Все файлы (*.*)|*.*'");
	Диалог.ПолноеИмяФайла = ИмяФайла;
	Если НЕ Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	ИмяФайла = Диалог.ПолноеИмяФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Элементы, Значение)
	Для Каждого Элемент из Элементы Цикл
		УстановитьПометки(Элемент.ПолучитьЭлементы(), Значение);
		Элемент.Выбран = Значение;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция СформироватьТабличныйДокумент()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВебСайт.ОтносительнаяСсылка КАК ОтносительнаяСсылка,
	|	ВебСайт.ТипФайла КАК ТипФайла,
	|	ВебСайт.АлгоритмОбработки КАК АлгоритмОбработки,
	|	ВебСайт.ХранилищеФайла КАК ХранилищеФайла,
	|	ВебСайт.СтраницаАвторизации КАК СтраницаАвторизации,
	|	ВебСайт.СтраницаДоступноБезАвторизации КАК СтраницаДоступноБезАвторизации,
	|	ВебСайт.Страница404 КАК Страница404,
	|	ВебСайт.СтраницаОсновная КАК СтраницаОсновная
	|ИЗ
	|	Справочник.ВебСайт КАК ВебСайт
	|ГДЕ
	|	НЕ ВебСайт.ЭтоГруппа
	|	И НЕ ВебСайт.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтносительнаяСсылка";
	Выборка = Запрос.Выполнить().Выбрать();	
	
	Макет = Обработки.ВебСайт_Шаблоны.ПолучитьМакет("Шаблон");	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Строка = Макет.ПолучитьОбласть("Файл");
	
	Результат = Новый ТабличныйДокумент;
	Результат.Вывести(Шапка);
	
	Пока Выборка.Следующий() цикл
		
		Строка.Параметры.ОтносительнаяСсылка		 	= Выборка.ОтносительнаяСсылка;	
		Строка.Параметры.ТипФайла					 	= Выборка.ТипФайла;
		Строка.Параметры.АлгоритмОбработки			 	= Выборка.АлгоритмОбработки;
		Строка.Параметры.ХранилищеФайла				 	= XMLСтрока(Выборка.ХранилищеФайла);
		Строка.Параметры.СтраницаАвторизации			= Формат(Выборка.СтраницаАвторизации,"БЛ=0; БИ=1");
		Строка.Параметры.СтраницаДоступноБезАвторизации = Формат(Выборка.СтраницаДоступноБезАвторизации,"БЛ=0; БИ=1");
		Строка.Параметры.Страница404		 			= Формат(Выборка.Страница404		 ,"БЛ=0; БИ=1");
		Строка.Параметры.СтраницаОсновная	 			= Формат(Выборка.СтраницаОсновная	 ,"БЛ=0; БИ=1");
		
		Результат.Вывести(Строка);
		
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОперацию(Команда)
	
	Если РежимЗагрузки = 2 тогда
		ТабличныйДокумент = СформироватьТабличныйДокумент();
		ТабличныйДокумент.Показать();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан путь для выгрузки/загрузки'"));
		Возврат;
	ИначеЕсли НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗагрузки = 0 Тогда
		ТекПуть = СокрЛП(ИмяФайла)+?(Прав(СокрП(ИмяФайла), 1)="\", "", "\");
		ЗагрузитьФайлы();
		Оповестить("ОбновитьСтраницыЛичногоКабинета");
		ПоказатьПредупреждение(, НСтр("ru = 'Загрузка завершена'"));
	ИначеЕсли РежимЗагрузки = 1 тогда
		ТекПуть = СокрЛП(ИмяФайла)+?(Прав(СокрП(ИмяФайла), 1)="\", "", "\");
		ВыгрузитьФайлы();
		ПоказатьПредупреждение(, НСтр("ru = 'Выгрузка завершена'"));		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)

	Ответ = Неопределено;
	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьЗавершение", ЭтотОбъект), НСтр("ru = 'Вы действительно хотите удалить ВСЕ страницы?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса; 
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьНаСервере();
	Оповестить("ОбновитьСтраницыЛичногоКабинета");
	ПоказатьПредупреждение(, НСтр("ru = 'Очистка завершена'"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ВыгрузитьНаСервере()
	
	Возврат Справочники.ВебСайт.ВыгрузитьСтраницы();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(Знач Адрес)
	
	Справочники.ВебСайт.ЗагрузитьСтраницы(Адрес);	
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузитьФайлы()

	 ДД		= Новый ДвоичныеДанные(ИмяФайла);
	 Адрес  = ПоместитьВоВременноеХранилище(ДД);
	 ЗагрузитьНаСервере(Адрес);
	
КонецФункции

&НаКлиенте
Функция ВыгрузитьФайлы()
	
	Адрес = ВыгрузитьНаСервере();
	ДД = ПолучитьИзВременногоХранилища(Адрес);
	ДД.Записать(ИмяФайла);
	УдалитьИзВременногоХранилища(Адрес);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОчиститьНаСервере()
	
	Справочники.ВебСайт.ОчиститьСтраницыИНастройки();
   
КонецПроцедуры

#КонецОбласти

